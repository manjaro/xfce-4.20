# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=exo
pkgver=4.19.2
pkgrel=1
pkgdesc="Application library for the Xfce desktop environment"
arch=('x86_64')
url="https://docs.xfce.org/xfce/exo/start"
license=('GPL-2.0-or-later AND LGPL-2.1-or-later')
groups=('xfce4')
depends=('libxfce4ui' 'hicolor-icon-theme')
makedepends=('git' 'glib2-devel' 'intltool' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/xfce/exo.git#tag=$pkgname-$pkgver")
sha256sums=('d7d2925704837c787d27d1ef6ec2e09e13fc1d22bc29bde9845977f9b6e9b9a1')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gtk-doc \
    --disable-debug \
    --enable-maintainer-mode
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
