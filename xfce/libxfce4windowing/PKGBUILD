# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=libxfce4windowing
pkgver=4.19.10
pkgrel=0
pkgdesc="Windowing concept abstraction library for X11 and Wayland"
arch=('x86_64')
url="https://docs.xfce.org/xfce/libxfce4windowing/start"
license=('LGPL-2.1-only')
depends=('gdk-pixbuf2' 'glib2' 'gtk3' 'libdisplay-info' 'libwnck3' 'libx11' 'wayland' 'wayland-protocols' 'wlr-protocols')
makedepends=('gobject-introspection' 'gtk-doc')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('356a08b92a04b887fefa0675cba705dc2e7d337d37f0ee6f28a7e9a1491e62d5')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gtk-doc \
    --enable-wayland \
    --enable-x11 \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
