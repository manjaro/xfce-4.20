# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=libxfce4util
pkgver=4.19.5
pkgrel=1
pkgdesc="Basic utility non-GUI functions for Xfce"
arch=('x86_64')
url="https://docs.xfce.org/xfce/libxfce4util/start"
license=('GPL-2.0-only')
depends=('glib2')
makedepends=('git' 'intltool' 'gobject-introspection' 'vala' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/xfce/libxfce4util.git#tag=$pkgname-$pkgver")
sha256sums=('764ae50113ee3db9773e98aff5dc48f08b8a6d575645792c0737b4c42ee2fe65')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --enable-gtk-doc \
    --disable-debug \
    --enable-maintainer-mode
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
