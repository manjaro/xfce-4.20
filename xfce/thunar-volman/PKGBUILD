# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=thunar-volman
pkgver=4.19.1
pkgrel=1
pkgdesc="Automatic management of removable drives and media for Thunar"
arch=('x86_64')
url="https://docs.xfce.org/xfce/thunar/thunar-volman"
license=('GPL-2.0-or-later')
groups=('xfce4')
depends=('thunar' 'libxfce4ui' 'hicolor-icon-theme')
makedepends=('git' 'intltool' 'xfce4-dev-tools')
source=("git+https://gitlab.xfce.org/xfce/thunar-volman.git#tag=$pkgname-$pkgver")
sha256sums=('0efd22fbd679a3bc8a8b887356d312f859ed6cfd916b8b5eb6687cef08036845')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
