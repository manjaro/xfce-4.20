# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=xfce4-settings
pkgver=4.19.4
pkgrel=1
pkgdesc="Xfce's Configuration System"
arch=('x86_64')
url="https://docs.xfce.org/xfce/xfce4-settings/start"
license=('GPL-2.0-or-later')
groups=('xfce4')
depends=('exo' 'garcon' 'libxfce4ui' 'xfconf' 'libnotify' 'libcanberra'
         'colord' 'libxklavier' 'elementary-icon-theme' 'gnome-themes-extra'
         'gtk-layer-shell' 'wayland')
makedepends=('git' 'glib2-devel' 'intltool' 'xfce4-dev-tools' 'xf86-input-libinput')
optdepends=('python: xfce4-compose-mail -- "mailto:" URI handling'
            'xiccd: for displays support in xfce4-color-settings'
            'cups: for printers support in xfce4-color-settings'
            'sane: for scanners support in xfce4-color-settings')
source=("git+https://gitlab.xfce.org/xfce/xfce4-settings.git#tag=$pkgname-$pkgver"
        default-xsettings-xml.patch)
sha256sums=('492d9218906b610cc804c8839954105e0d7185583452bb6c6f25b1f862799033'
            '1d5344a4ac5d7211f3385f96fb0ff9a5a0a2a24610191454adbcea19af7c04c0')
prepare() {
  cd $pkgname

  # Enable Adwaita theme, elementary icon theme and font hinting by default
  patch -Np1 -i "$srcdir/default-xsettings-xml.patch"

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-xrandr \
    --enable-xcursor \
    --enable-libnotify \
    --enable-libxklavier \
    --enable-pluggable-dialogs \
    --enable-sound-settings \
    --disable-debug \
    --enable-maintainer-mode
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
